// RUN: %target-swift-frontend -emit-sil -O %s | %FileCheck %s

import Builtin
import Swift

sil_stage raw

sil hidden @foo_prim : $@convention(thin) (Float) -> (Float, Float) {
bb0(%0 : @trivial $Float):
  %1 = tuple (%0 : $Float, %0 : $Float)
  return %1 : $(Float, Float)
}

sil hidden @foo_adj : $@convention(thin) (Float, Float, Float, Float) -> Float {
bb0(%0 : @trivial $Float, %1 : @trivial $Float, %2 : @trivial $Float, %3 : @trivial $Float):
  return %3 : $Float
}

sil hidden [reverse_differentiable source 0 wrt 0 primal @foo_prim adjoint @foo_adj] @foo : $@convention(thin) (Float) -> Float {
bb0(%0 : @trivial $Float):
  return %0 : $Float
}

// Nested function being called by `@func_to_diff`.
sil hidden @nested_func_without_diffattr : $@convention(thin) (Float) -> Float {
bb0(%0 : @trivial $Float):
  %1 = function_ref @foo : $@convention(thin) (Float) -> Float
  %2 = apply %1(%0) : $@convention(thin) (Float) -> Float
  return %2 : $Float
}

// Main function to differentiate.
sil hidden [reverse_differentiable source 0 wrt 0] @func_to_diff : $@convention(thin) (Float) -> Float {
bb0(%0 : @trivial $Float):
  %1 = function_ref @nested_func_without_diffattr : $@convention(thin) (Float) -> Float
  %2 = apply %1(%0) : $@convention(thin) (Float) -> Float
  %3 = function_ref @nested_func_without_diffattr : $@convention(thin) (Float) -> Float
  %4 = apply %3(%2) : $@convention(thin) (Float) -> Float
  %5 = tuple (%2 : $Float, %2 : $Float)
  %6 = tuple_extract %5 : $(Float, Float), 0
  return %6 : $Float
}

// CHECK-LABEL: struct func_to_diff__Type {
// CHECK:   @sil_stored var pv_0: nested_func_without_diffattr__Type
// CHECK:   @sil_stored var v_0: Float
// CHECK: }

// CHECK-LABEL: struct nested_func_without_diffattr__Type {
// CHECK:   @sil_stored var pv_0: Float
// CHECK:   @sil_stored var v_0: Float
// CHECK: }

// CHECK-LABEL: [reverse_differentiable source 0 wrt 0 primal @nested_func_without_diffattr__primal_src_0_wrt_0 adjoint @nested_func_without_diffattr__adjoint_src_0_wrt_0] @nested_func_without_diffattr : $@convention(thin) (Float) -> Float {
// CHECK: bb0(%0 : $Float):
// CHECK:   return %0 : $Float
// CHECK: }

// CHECK-LABEL: [reverse_differentiable source 0 wrt 0 primal @func_to_diff__primal_src_0_wrt_0 adjoint @func_to_diff__adjoint_src_0_wrt_0] @func_to_diff : $@convention(thin) (Float) -> Float {
// CHECK: bb0(%0 : $Float):
// CHECK:   return %0 : $Float
// CHECK: }

// CHECK-LABEL: @func_to_diff__primal_src_0_wrt_0 : $@convention(thin) (Float) -> (@owned func_to_diff__Type, Float) {
// CHECK: bb0(%0 : $Float):
// CHECK:   %1 = struct $nested_func_without_diffattr__Type (%0 : $Float, %0 : $Float)
// CHECK:   %2 = struct $func_to_diff__Type (%1 : $nested_func_without_diffattr__Type, %0 : $Float)
// CHECK:   %3 = tuple (%2 : $func_to_diff__Type, %0 : $Float)
// CHECK:   return %3 : $(func_to_diff__Type, Float)
// CHECK: }

// CHECK-LABEL: @nested_func_without_diffattr__primal_src_0_wrt_0 : $@convention(thin) (Float) -> (@owned nested_func_without_diffattr__Type, Float) {
// CHECK: bb0(%0 : $Float):
// CHECK:   %1 = struct $nested_func_without_diffattr__Type (%0 : $Float, %0 : $Float)
// CHECK:   %2 = tuple (%1 : $nested_func_without_diffattr__Type, %0 : $Float)
// CHECK:   return %2 : $(nested_func_without_diffattr__Type, Float)
// CHECK: }

// CHECK-LABEL: @func_to_diff__adjoint_src_0_wrt_0 : $@convention(thin) (Float, func_to_diff__Type, Float, Float) -> Float {
// CHECK: bb0(%0 : $Float, %1 : $func_to_diff__Type, %2 : $Float, %3 : $Float):
// CHECK:   return %3 : $Float
// CHECK: }

// CHECK-LABEL: @nested_func_without_diffattr__adjoint_src_0_wrt_0 : $@convention(thin) (Float, nested_func_without_diffattr__Type, Float, Float) -> Float {
// CHECK: bb0(%0 : $Float, %1 : $nested_func_without_diffattr__Type, %2 : $Float, %3 : $Float):
// CHECK:   return %3 : $Float
// CHECK: }
