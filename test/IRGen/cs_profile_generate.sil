// RUN: %target-swift-frontend -cs-profile-generate -O -emit-ir %s | %FileCheck %s

sil_stage canonical

import Builtin
import Swift
import SwiftShims

sil @b : $@convention(thin) () -> ()

sil @c : $@convention(thin) () -> ()


// CHECK: @__llvm_profile_runtime = external hidden global

// counter array (2 counters for the then/else)
// CHECK: @__profc_a = {{.*}} global [2 x i64]
// data record pointing at the counter array and the function
// CHECK: @__profd_a = {{.*}} global {{.*}} ptr @a.local


// CHECK: br i1 {{.*}}, label %[[THEN:[0-9]+]], label %[[ELSE:[0-9]+]]
// THEN: increment counter[0] and call b()
// CHECK: [[THEN]]:
// CHECK:   %[[L0:.*]] = load i64, ptr @__profc_a, align 8
// CHECK:   %[[A0:.*]] = add i64 %[[L0]], 1
// CHECK:   store i64 %[[A0]], ptr @__profc_a, align 8
// CHECK:   call swiftcc void @b()
// CHECK:   br label %[[MERGE:[0-9]+]]

// ELSE: increment counter[1] and call c()
// CHECK: [[ELSE]]:
// CHECK:   %[[L1:.*]] = load i64, ptr getelementptr inbounds (i8, ptr @__profc_a, i64 8), align 8
// CHECK:   %[[A1:.*]] = add i64 %[[L1]], 1
// CHECK:   store i64 %[[A1]], ptr getelementptr inbounds (i8, ptr @__profc_a, i64 8), align 8
// CHECK:   call swiftcc void @c()
// CHECK:   br label %[[MERGE]]

// CHECK: [[MERGE]]:
// CHECK:   ret void

// CHECK: declare swiftcc void @c()
// CHECK: declare swiftcc void @b()

sil @a : $@convention(thin) (Bool) -> () {
bb0(%0 : $Bool):
  %2 = struct_extract %0, #Bool._value
  cond_br %2, bb1, bb2

bb1:
  // function_ref b()
  %4 = function_ref @b : $@convention(thin) () -> ()
  %5 = apply %4() : $@convention(thin) () -> ()
  br bb3 

bb2:
  // function_ref c()
  %7 = function_ref @c: $@convention(thin) () -> ()
  %8 = apply %7() : $@convention(thin) () -> ()
  br bb3 

bb3:
  %10 = tuple ()
  return %10
}
