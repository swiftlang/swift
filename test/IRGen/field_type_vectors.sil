// RUN: %target-swift-frontend %s -emit-ir | FileCheck %s

// REQUIRES: CPU=x86_64
// XFAIL: linux

import Swift

// CHECK-LABEL: @_TMnV18field_type_vectors3Foo = constant 
// CHECK:         %swift.type** (%swift.type*)* [[FOO_TYPES_ACCESSOR:@[^,]*]],
struct Foo {
  var x: Int
}

// CHECK-LABEL: @_TMnV18field_type_vectors3Bar = constant
// CHECK:         %swift.type** (%swift.type*)* [[BAR_TYPES_ACCESSOR:@[^,]*]],
// CHECK-LABEL: @_TMPdV18field_type_vectors3Bar = global
// -- There should be 5 words between the address point and the field type
//    vector slot, with type %swift.type**
// CHECK:         i64, { {{[^}]*}} }*, %swift.type*, i64, %swift.type*, %swift.type**
struct Bar<T> {
  var y: Int
}

// CHECK-LABEL: @_TMnV18field_type_vectors3Bas = constant
// CHECK:         %swift.type** (%swift.type*)* [[BAS_TYPES_ACCESSOR:@[^,]*]],
// CHECK-LABEL: @_TMPdV18field_type_vectors3Bas = global
// -- There should be 7 words between the address point and the field type
//    vector slot, with type %swift.type**
// CHECK:         i64, { {{[^}]*}} }*, %swift.type*, i64, i64, %swift.type*, %swift.type*, %swift.type**
struct Bas<T, U> {
  var foo: Foo
  var bar: Bar<T>
}

// CHECK-LABEL: @_TMnC18field_type_vectors3Zim = constant
// CHECK:         %swift.type** (%swift.type*)* [[ZIM_TYPES_ACCESSOR:@[^,]*]],
// CHECK-LABEL: @_TMPdC18field_type_vectors3Zim = global
// -- There should be 14 words between the address point and the field type
//    vector slot, with type %swift.type**
// CHECK:         i64, %objc_class*, %swift.opaque*, %swift.opaque*, i64, i32, i32, i32, i16, i16, i32, i32, { {{[^}]*}} }*, %swift.type*, %swift.type*, i8*, i64, i64, i64, %swift.type**
class Zim<T, U> {
  var foo: Foo? = nil
  var bar: Bar<T>? = nil
  var bas: Bas<T, U>? = nil
}
sil_vtable Zim {}

sil @_TFC18field_type_vectors3ZimcU___fMGS0_Q_Q0__FT_GS0_Q_Q0__ : $@cc(method) @thin <T, U> (@owned Zim<T, U>) -> @owned Zim<T, U>

// CHECK-LABEL: @_TMnC18field_type_vectors4Zang = constant
// CHECK:         %swift.type** (%swift.type*)* [[ZANG_TYPES_ACCESSOR:@[^,]*]],
// CHECK-LABEL: @_TMPdC18field_type_vectors4Zang = global
// -- There should be 16 words between the address point and the field type
//    vector slot, with type %swift.type**
// CHECK:         i64, %swift.type*, %swift.opaque*, %swift.opaque*, i64, i32, i32, i32, i16, i16, i32, i32, { {{[^}]*}} }*, %swift.type*, %swift.type*, i8*, i64, i64, i64, %swift.type*, i64, %swift.type**
class Zang<V>: Zim<V, V> {
  var zung: Int = 0
}
sil_vtable Zang {}

sil @_TFC18field_type_vectors4ZangcU__fMGS0_Q__FT_GS0_Q__ : $@cc(method) @thin <V> (@owned Zang<V>) -> @owned Zang<V>

// -- For storage-qualified fields, we just report the formal type.
class StorageQualified {
  unowned var a: StorageQualified
  weak var b: StorageQualified?

  init(x: StorageQualified) {
    a = x
    b = x
  }
}
sil_vtable StorageQualified {}

// CHECK:       [[FOO_TYPE_VECTOR_SLOT:@.*Foo.*]] = private global %swift.type** null

// CHECK: define private %swift.type** [[FOO_TYPES_ACCESSOR]](%swift.type*)
// CHECK:   [[EXISTING:%.*]] = load %swift.type**, %swift.type*** [[FOO_TYPE_VECTOR_SLOT]]
// CHECK:   [[IS_NULL:%.*]] = icmp eq %swift.type** [[EXISTING]], null
// CHECK:   br i1 [[IS_NULL]], label %[[BUILD_FIELD_TYPES:.*]], label %[[DONE:.*]]
// CHECK: [[BUILD_FIELD_TYPES]]:
// CHECK:   store {{.*}} @_TMdSi
// CHECK:   cmpxchg {{.*}} [[FOO_TYPE_VECTOR_SLOT]]

// CHECK: define private %swift.type** [[BAR_TYPES_ACCESSOR]](%swift.type*)
// CHECK:   [[T0:%.*]] = bitcast %swift.type* %0 to %swift.type***
// -- 5 words between the address point and the slot
// CHECK:   [[SLOT:%.*]] = getelementptr inbounds %swift.type**, %swift.type*** [[T0]], i32 5
// CHECK:   load %swift.type**, %swift.type*** [[SLOT]], align 8
// CHECK:   br
// CHECK:   store {{.*}} @_TMdSi


// CHECK: define private %swift.type** [[BAS_TYPES_ACCESSOR]](%swift.type*)
// CHECK:   [[T0:%.*]] = bitcast %swift.type* %0 to %swift.type***
// -- 7 words between the address point and the slot
// CHECK:   [[SLOT:%.*]] = getelementptr inbounds %swift.type**, %swift.type*** [[T0]], i32 7
// CHECK:   load %swift.type**, %swift.type*** [[SLOT]], align 8
// CHECK:   br
// CHECK:   store {{.*}} @_TMdV18field_type_vectors3Foo
// CHECK:   [[T:%.*]] = bitcast %swift.type* %T to i8*
// CHECK:   call %swift.type* @swift_getGenericMetadata1({{.*}} @_TMPdV18field_type_vectors3Bar {{.*}}, i8* [[T]])

// CHECK: define private %swift.type** [[ZIM_TYPES_ACCESSOR]](%swift.type*)
// CHECK:   [[T0:%.*]] = bitcast %swift.type* %0 to %swift.type***
// -- 14 words between the address point and the slot
// CHECK:   [[SLOT:%.*]] = getelementptr inbounds %swift.type**, %swift.type*** [[T0]], i32 15
// CHECK:   load %swift.type**, %swift.type*** [[SLOT]], align 8

// CHECK: define private %swift.type** [[ZANG_TYPES_ACCESSOR]](%swift.type*)
// CHECK:   [[T0:%.*]] = bitcast %swift.type* %0 to %swift.type***
// -- 16 words between the address point and the slot
// CHECK:   [[SLOT:%.*]] = getelementptr inbounds %swift.type**, %swift.type*** [[T0]], i32 17
// CHECK:   load %swift.type**, %swift.type*** [[SLOT]], align 8

