// RUN: %empty-directory(%t)
// RUN: %target-build-swift-dylib(%t/%target-library-name(PrintShims)) %S/../../Inputs/print-shims.swift -module-name PrintShims -emit-module -emit-module-path %t/PrintShims.swiftmodule
// RUN: %target-codesign %t/%target-library-name(PrintShims)
// RUN: %target-build-swift  -g -parse-sil %s -emit-ir -I %t -L %t -lPrintShim | %FileCheck %s --check-prefix=CHECK-LL
// RUN: %target-build-swift  -g -parse-sil %s -module-name main -o %t/main -I %t -L %t -lPrintShims %target-rpath(%t)
// RUN: %target-codesign %t/main
// RUN: %target-run %t/main %t/%target-library-name(PrintShims) | %FileCheck %s

// REQUIRES: executable_test
// REQUIRES: swift_test_mode_optimize_none
// REQUIRES: concurrency
// REQUIRES: concurrency_runtime
// UNSUPPORTED: back_deployment_runtime


import Builtin
import Swift
import PrintShims
import _Concurrency

sil public_external @printAny : $@convention(thin) (@in_guaranteed Any) -> ()

public protocol P {
}

public struct S : P {
  @_hasStorage let int: Int64 { get }
  init(int: Int64)
}

sil_witness_table [serialized] S: P module main {
}

sil hidden @S_init : $@convention(method) (Int64, @thin S.Type) -> S {
bb0(%int : $Int64, %S_type : $@thin S.Type):
  %instance = struct $S (%int : $Int64)
  return %instance : $S
}

// CHECK-LL: @voidToExistentialTu =
// CHECK-LL: define hidden swift{{(tail)?}}cc void @voidToExistential(
sil hidden @voidToExistential : $@async @convention(thin) () -> @out P {
bb0(%out : $*P):
  %S_type = metatype $@thin S.Type
  %int_literal = integer_literal $Builtin.Int64, 42
  %int = struct $Int64 (%int_literal : $Builtin.Int64)
  %S_init = function_ref @S_init : $@convention(method) (Int64, @thin S.Type) -> S
  %instance = apply %S_init(%int, %S_type) : $@convention(method) (Int64, @thin S.Type) -> S
  %existential = alloc_stack $P
  %existential_addr = init_existential_addr %existential : $*P, $S
  store %instance to %existential_addr : $*S
  copy_addr %existential to [init] %out : $*P
  destroy_addr %existential : $*P
  dealloc_stack %existential : $*P
  %result = tuple ()
  return %result : $()
}

sil hidden @call : $@async @convention(thin) () -> () {
bb0:
  %existential = alloc_stack $P
  %voidToExistential = function_ref @voidToExistential : $@async @convention(thin) () -> @out P
  %voidToExistential_result = apply %voidToExistential(%existential) : $@async @convention(thin) () -> @out P
  %existential_addr = open_existential_addr immutable_access %existential : $*P to $*@opened("1819CC6E-FEC6-11EA-876D-D0817AD71B77", P) Self
  %any = alloc_stack $Any
  %any_addr = init_existential_addr %any : $*Any, $@opened("1819CC6E-FEC6-11EA-876D-D0817AD71B77", P) Self
  copy_addr %existential_addr to [init] %any_addr : $*@opened("1819CC6E-FEC6-11EA-876D-D0817AD71B77", P) Self
  %printAny = function_ref @printAny : $@convention(thin) (@in_guaranteed Any) -> ()
  %result = apply %printAny(%any) : $@convention(thin) (@in_guaranteed Any) -> ()
  destroy_addr %any : $*Any
  dealloc_stack %any : $*Any
  destroy_addr %existential : $*P
  dealloc_stack %existential : $*P
  return %result : $()
}

sil @test_case : $@convention(thin) @async () -> () {
  %call = function_ref @call : $@async @convention(thin) () -> ()
  %result = apply %call() : $@async @convention(thin) () -> () // CHECK: S(int: 42)

  %void = tuple()
  return %void : $()
}

// Defined in _Concurrency
sil @$ss13_runAsyncMainyyyyYaKcF : $@convention(thin) (@guaranteed @async @callee_guaranteed () -> @error Error) -> ()

sil @no_throw_to_throw_think : $@convention(thin) @async (@guaranteed @async @callee_guaranteed () -> ()) -> @error Error {
bb0(%0 :$@async @callee_guaranteed () -> ()):
  %void = apply %0() : $@async @callee_guaranteed () -> ()
  return %void : $()
}

// main
sil @main : $@convention(c) () -> Int32 {
  %test_case_nothrow = function_ref @test_case : $@convention(thin) @async () -> ()
  %thick_test_case = thin_to_thick_function %test_case_nothrow : $@convention(thin) @async () -> () to $@callee_guaranteed @async () -> ()
  %thunk = function_ref @no_throw_to_throw_think : $@convention(thin) @async (@guaranteed @async @callee_guaranteed () -> ()) -> @error Error
  %throwing = partial_apply [callee_guaranteed] %thunk(%thick_test_case) : $@convention(thin) @async (@guaranteed @async @callee_guaranteed () -> ()) -> @error Error
  %runAsyncMain = function_ref @$ss13_runAsyncMainyyyyYaKcF : $@convention(thin) (@guaranteed @async @callee_guaranteed () -> @error Error) -> ()
  %result = apply %runAsyncMain(%throwing) : $@convention(thin) (@guaranteed @async @callee_guaranteed () -> @error Error) -> ()
  %out_literal = integer_literal $Builtin.Int32, 0
  %out = struct $Int32 (%out_literal : $Builtin.Int32)
  return %out : $Int32
}
