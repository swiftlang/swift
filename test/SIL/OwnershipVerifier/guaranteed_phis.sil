// RUN: %target-sil-opt -enable-sil-verify-all %s

import Swift
import Builtin

class SuperKlass {}

class Klass : SuperKlass {}

struct Wrapper1 {
  var val1: Wrapper2
}

struct Wrapper2 {
  var val2: Klass
}

struct TStruct {
}

struct KlassAndInt {
  let c: Klass
  let i: Int
}


sil @use_superklass : $@convention(thin) (@guaranteed SuperKlass) -> ()
sil @use_klass : $@convention(thin) (@guaranteed Klass) -> ()

sil @use_wrapper2 : $@convention(thin) (@guaranteed Wrapper2) -> ()

sil [ossa] @test_forwarded_phi1 : $@convention(thin) (@owned Klass) -> () {
bb0(%0 : @owned $Klass):
  %1 = begin_borrow %0
  br bb1

bb1:
  %3 = unchecked_ref_cast %1 to $SuperKlass
  br bb2(%3)

bb2(%5 : @guaranteed $SuperKlass):
  %6 = borrowed %5 from (%1)
  %7 = function_ref @use_superklass : $@convention(thin) (@guaranteed SuperKlass) -> ()
  %8 = apply %7(%6) : $@convention(thin) (@guaranteed SuperKlass) -> ()
  end_borrow %1
  destroy_value %0
  %11 = tuple ()
  return %11
}

sil [ossa] @test_forwarded_phi2 : $@convention(thin) (@owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1):
  %1 = begin_borrow %0
  br bb1

bb1:
  %3 = struct_extract %1, #Wrapper1.val1
  br bb2(%3)

bb2(%5 : @guaranteed $Wrapper2):
  %6 = borrowed %5 from (%1)
  %7 = struct_extract %6, #Wrapper2.val2
  br bb3(%7)

bb3(%9 : @guaranteed $Klass):
  %10 = borrowed %9 from (%1)
  %11 = function_ref @use_klass : $@convention(thin) (@guaranteed Klass) -> ()
  %12 = apply %11(%10) : $@convention(thin) (@guaranteed Klass) -> ()
  end_borrow %1
  destroy_value %0
  %15 = tuple ()
  return %15
}

sil [ossa] @test_forwarded_phi3 : $@convention(thin) (@owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1):
  %1 = begin_borrow %0
  br bb1

bb1:
  %3 = struct_extract %1, #Wrapper1.val1
  br bb2(%3, %1)

bb2(%5 : @guaranteed $Wrapper2, %6 : @reborrow $Wrapper1):
  %7 = borrowed %6 from (%0)
  %8 = borrowed %5 from (%6)
  %9 = struct_extract %8, #Wrapper2.val2
  br bb3(%9)

bb3(%11 : @guaranteed $Klass):
  %12 = borrowed %11 from (%6)
  %13 = function_ref @use_klass : $@convention(thin) (@guaranteed Klass) -> ()
  %14 = apply %13(%12) : $@convention(thin) (@guaranteed Klass) -> ()
  end_borrow %7
  destroy_value %0
  %17 = tuple ()
  return %17
}


sil [ossa] @test_forwarded_phi4 : $@convention(thin) (@owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1):
  %1 = begin_borrow %0
  br bb1

bb1:
  %3 = struct_extract %1, #Wrapper1.val1
  br bb2(%3, %1)

bb2(%5 : @guaranteed $Wrapper2, %6 : @reborrow $Wrapper1):
  %7 = borrowed %6 from (%0)
  %8 = borrowed %5 from (%6)
  %9 = struct_extract %8, #Wrapper2.val2
  br bb3(%9, %7)

bb3(%11 : @guaranteed $Klass, %12 : @reborrow $Wrapper1):
  %13 = borrowed %12 from (%0)
  %14 = borrowed %11 from (%12)
  %15 = function_ref @use_klass : $@convention(thin) (@guaranteed Klass) -> ()
  %16 = apply %15(%14) : $@convention(thin) (@guaranteed Klass) -> ()
  end_borrow %13
  destroy_value %0
  %19 = tuple ()
  return %19
}

sil [ossa] @test_forwarded_phi5 : $@convention(thin) (@owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1):
  %1 = begin_borrow %0
  br bb1

bb1:
  %3 = struct_extract %1, #Wrapper1.val1
  br bb2(%3, %1, %0)

bb2(%5 : @guaranteed $Wrapper2, %6 : @reborrow $Wrapper1, %7 : @owned $Wrapper1):
  %8 = borrowed %6 from (%7)
  %9 = borrowed %5 from (%6)
  %10 = struct_extract %9, #Wrapper2.val2
  br bb3(%10, %8, %7)

bb3(%12 : @guaranteed $Klass, %13 : @reborrow $Wrapper1, %14 : @owned $Wrapper1):
  %15 = borrowed %13 from (%14)
  %16 = borrowed %12 from (%13)
  %17 = function_ref @use_klass : $@convention(thin) (@guaranteed Klass) -> ()
  %18 = apply %17(%16) : $@convention(thin) (@guaranteed Klass) -> ()
  end_borrow %15
  destroy_value %14
  %21 = tuple ()
  return %21
}

sil [ossa] @test_forwarded_phi6 : $@convention(thin) (@owned Wrapper1, @owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1, %1 : @owned $Wrapper1):
  cond_br undef, bb1, bb2

bb1:
  %3 = begin_borrow %0
  %4 = struct_extract %3, #Wrapper1.val1
  br bb3(%4, %3)

bb2:
  %6 = begin_borrow %1
  %7 = struct_extract %6, #Wrapper1.val1
  br bb3(%7, %6)

bb3(%9 : @guaranteed $Wrapper2, %10 : @reborrow $Wrapper1):
  %11 = borrowed %10 from (%1, %0)
  %12 = borrowed %9 from (%10)
  %13 = struct_extract %12, #Wrapper2.val2
  br bb4(%13, %11)

bb4(%15 : @guaranteed $Klass, %16 : @reborrow $Wrapper1):
  %17 = borrowed %16 from (%0, %1)
  %18 = borrowed %15 from (%16)
  %19 = function_ref @use_klass : $@convention(thin) (@guaranteed Klass) -> ()
  %20 = apply %19(%18) : $@convention(thin) (@guaranteed Klass) -> ()
  end_borrow %17
  destroy_value %0
  destroy_value %1
  %24 = tuple ()
  return %24
}

sil [ossa] @test_forwarded_phi7 : $@convention(thin) (@owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1):
  %1 = begin_borrow %0
  %2 = struct_extract %1, #Wrapper1.val1
  br bb1(%2, %1)

bb1(%4 : @guaranteed $Wrapper2, %5 : @reborrow $Wrapper1):
  %6 = borrowed %5 from (%0)
  %7 = borrowed %4 from (%5)
  %8 = function_ref @use_wrapper2 : $@convention(thin) (@guaranteed Wrapper2) -> ()
  %9 = apply %8(%7) : $@convention(thin) (@guaranteed Wrapper2) -> ()
  %10 = struct_extract %6, #Wrapper1.val1
  br bb2(%10, %6)

bb2(%12 : @guaranteed $Wrapper2, %13 : @reborrow $Wrapper1):
  %14 = borrowed %13 from (%0)
  %15 = borrowed %12 from (%13)
  %16 = apply %8(%15) : $@convention(thin) (@guaranteed Wrapper2) -> ()
  end_borrow %14
  destroy_value %0
  %19 = tuple ()
  return %19
}

sil [ossa] @test_forwarded_phi8 : $@convention(method) (@guaranteed Wrapper1) -> (UInt64, UInt64) {
bb0(%0 : @guaranteed $Wrapper1):
  %1 = struct_extract %0, #Wrapper1.val1
  %2 = begin_borrow %0
  %3 = struct_extract %2, #Wrapper1.val1
  br bb1(%3, %2)

bb1(%5 : @guaranteed $Wrapper2, %6 : @reborrow $Wrapper1):
  %7 = borrowed %6 from (%0)
  %8 = borrowed %5 from (%6)
  %9 = copy_value %8
  %10 = unchecked_trivial_bit_cast %9 to $UInt64
  destroy_value %9
  end_borrow %7
  %13 = tuple (%10, %10)
  return %13
}


sil [ossa] @test_forwarded_phi9 : $@convention(thin) (@owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1):
  %1 = begin_borrow %0
  %2 = struct_extract %1, #Wrapper1.val1
  %3 = struct_extract %2, #Wrapper2.val2
  %4 = function_ref @use_klass : $@convention(thin) (@guaranteed Klass) -> ()
  %5 = apply %4(%3) : $@convention(thin) (@guaranteed Klass) -> ()
  end_borrow %1
  destroy_value %0
  %8 = tuple ()
  return %8
}

sil [ossa] @test_forwarding_phi10 : $@convention(thin) (@owned Wrapper2) -> (TStruct, TStruct) {
bb0(%0 : @owned $Wrapper2):
  %1 = begin_borrow %0
  %2 = struct_extract %1, #Wrapper2.val2
  br bb1(%2, %1)

bb1(%4 : @guaranteed $Klass, %5 : @reborrow $Wrapper2):
  %6 = borrowed %5 from (%0)
  %7 = borrowed %4 from (%5)
  %8 = copy_value %7
  %9 = unchecked_trivial_bit_cast %8 to $TStruct
  destroy_value %8
  end_borrow %6
  destroy_value %0
  %13 = tuple (%9, %9)
  return %13
}

sil [ossa] @test_cycle1 : $@convention(thin) (@guaranteed Klass, @guaranteed Klass) -> () {
bb0(%0 : @guaranteed $Klass, %1 : @guaranteed $Klass):
  cond_br undef, bb1, bb2

bb1:
  %3 = begin_borrow %0
  br bb3(%3)

bb2:
  %5 = begin_borrow %1
  br bb3(%5)

bb3(%7 : @reborrow $Klass):
  %8 = borrowed %7 from (%0, %1)
  cond_br undef, bb4, bb5

bb4:
  end_borrow %8
  %11 = begin_borrow %0
  br bb3(%11)

bb5:
  end_borrow %8
  %14 = tuple ()
  return %14
}


sil [ossa] @test_cycle2 : $@convention(thin) (@owned Klass) -> () {
bb0(%0 : @owned $Klass):
  %1 = begin_borrow %0
  br bb1(%1)

bb1(%3 : @reborrow $Klass):
  %4 = borrowed %3 from (%0)
  cond_br undef, bb2, bb3

bb2:
  br bb1(%4)

bb3:
  end_borrow %4
  destroy_value %0
  %9 = tuple ()
  return %9
}


sil [ossa] @test_cycle3 : $@convention(thin) (@owned Klass) -> () {
bb0(%0 : @owned $Klass):
  %1 = alloc_stack $Klass
  store %0 to [init] %1
  %3 = load_borrow %1
  br bb1(%3)

bb1(%5 : @reborrow $Klass):
  %6 = borrowed %5 from ()
  cond_br undef, bb2, bb3

bb2:
  br bb1(%6)

bb3:
  end_borrow %6
  destroy_addr %1
  dealloc_stack %1
  %12 = tuple ()
  return %12
}

sil [ossa] @test_reborrow : $@convention(thin) (@owned Klass, @owned Klass) -> () {
bb0(%0 : @owned $Klass, %1 : @owned $Klass):
  cond_br undef, bb1, bb2

bb1:
  %3 = begin_borrow %0
  br bb3(%3)

bb2:
  %5 = begin_borrow %0
  br bb3(%5)

bb3(%7 : @reborrow $Klass):
  %8 = borrowed %7 from (%0)
  end_borrow %8
  br bb4

bb4:
  destroy_value %0
  destroy_value %1
  %13 = tuple ()
  return %13
}

sil [ossa] @test_forwarded_separate_base_values : $@convention(thin) (@owned Wrapper1) -> () {
bb0(%0 : @owned $Wrapper1):
  %1 = begin_borrow %0
  cond_br undef, bb1, bb2

bb1:
  %3 = begin_borrow %0
  %4 = struct_extract %1, #Wrapper1.val1
  br bb3(%4, %3)

bb2:
  %6 = begin_borrow %0
  %7 = struct_extract %6, #Wrapper1.val1
  br bb3(%7, %6)

bb3(%9 : @guaranteed $Wrapper2, %10 : @reborrow $Wrapper1):
  %11 = borrowed %10 from (%0)
  %12 = borrowed %9 from (%10, %1)
  %13 = function_ref @use_wrapper2 : $@convention(thin) (@guaranteed Wrapper2) -> ()
  %14 = apply %13(%12) : $@convention(thin) (@guaranteed Wrapper2) -> ()
  end_borrow %11
  end_borrow %1
  destroy_value %0
  %18 = tuple ()
  return %18
}

sil hidden [ossa] @borrowed_from_reborrow : $@convention(thin) (@inout Klass) -> () {
bb0(%0 : $*Klass):
  %1 = load_borrow %0
  %2 = begin_borrow [lexical] %1
  br bb1(%1, %2)

bb1(%4 : @reborrow $Klass, %5 : @reborrow $Klass):
  %6 = borrowed %5 from (%4)
  %7 = borrowed %4 from ()
  br bb2(%7, %6)

bb2(%9 : @reborrow $Klass, %10 : @reborrow $Klass):
  %11 = borrowed %10 from (%9)
  %12 = borrowed %9 from ()
  br bb3(%12, %11)

bb3(%14 : @reborrow $Klass, %15 : @reborrow $Klass):
  %16 = borrowed %15 from (%14)
  %17 = borrowed %14 from ()
  end_borrow %16
  end_borrow %17
  %20 = tuple ()
  return %20
}

sil [ossa] @borrowed_from_trivial_enum : $@convention(thin) () -> () {
bb0:
  %0 = enum $Optional<Klass>, #Optional.none!enumelt
  %1 = begin_borrow %0
  br bb1(%1)

bb1(%3 : @reborrow $Optional<Klass>):
  %4 = borrowed %3 from (%0)
  end_borrow %4
  %r = tuple ()
  return %r
}

sil [ossa] @dont_follow_trivial_field_to_phi : $@convention(thin) (@owned KlassAndInt, @guaranteed Klass) -> () {
bb0(%0 : @owned $KlassAndInt, %1 : @guaranteed $Klass):
  %2 = begin_borrow %0
  %3 = struct_extract %2 , #KlassAndInt.i
  end_borrow %2
  destroy_value %0
  %6 = struct $KlassAndInt (%1, %3)
  br bb2(%6)

bb2(%8 : @guaranteed $KlassAndInt):
  %9 = borrowed %8 from (%1)
  %10 = apply undef(%9) : $@convention(thin) (@guaranteed KlassAndInt) -> ()
  %r = tuple ()
  return %r
}

