// RUN: %target-sil-opt -test-runner %s 2>&1 | %FileCheck %s

enum FakeOptional<T> {
  case some(T)
  case none
}

class SuperKlass {
}

class Klass : SuperKlass {
}

sil [ossa] @use_klass : $@convention(thin) (@guaranteed Klass) -> ()

// Test we don't get an assert while replacing branch target of checked_cast_br
sil [ossa] @test_cfg1 : $@convention(thin) (@guaranteed SuperKlass) -> @out FakeOptional<Klass> {
bb0(%0 : $*FakeOptional<Klass>, %1 : @guaranteed $SuperKlass):
  %2 = init_enum_data_addr %0 : $*FakeOptional<Klass>, #FakeOptional.some!enumelt
  checked_cast_br SuperKlass in %1 : $SuperKlass to Klass, bb1, bb2

bb1(%6 : @guaranteed $Klass):
  %7 = enum $FakeOptional<Klass>, #FakeOptional.some!enumelt, %6 : $Klass
  br bb3(%7 : $FakeOptional<Klass>)

bb2(%9 : @guaranteed $SuperKlass):
  %10 = enum $FakeOptional<Klass>, #FakeOptional.none!enumelt
  br bb3(%10 : $FakeOptional<Klass>)


bb3(%12 : @guaranteed $FakeOptional<Klass>):
  switch_enum %12 : $FakeOptional<Klass>, case #FakeOptional.some!enumelt: bb4, case #FakeOptional.none!enumelt: bb6

bb4(%14 : @guaranteed $Klass):
  %15 = copy_value %14 : $Klass
  store %15 to [init] %2 : $*Klass
  inject_enum_addr %0 : $*FakeOptional<Klass>, #FakeOptional.some!enumelt
  br bb5

bb5:
  %21 = tuple ()
  return %21 : $()

bb6:
  inject_enum_addr %0 : $*FakeOptional<Klass>, #FakeOptional.none!enumelt
  br bb5
}

enum E1<T> {
  case some1(T)
  case some2(T)
}

// CHECK-LABEL: sil [ossa] @test_simplify_term_with_identical_dest_blocks1 :
// CHECK: bb0
// CHECK: br bb2
// CHECK: bb1
// CHECK-LABEL: } // end sil function 'test_simplify_term_with_identical_dest_blocks1'
sil [ossa] @test_simplify_term_with_identical_dest_blocks1 : $@convention(thin) (@owned Klass) -> () {
bb0(%0 : @owned $Klass):
  test_specification "simplify-cfg-simplify-term-with-identical-dest-blocks @block[0]"
  %1 = begin_borrow %0 : $Klass
  br bb1(%1 : $Klass)

bb1(%3 : @guaranteed $Klass):
  br bb2(%3 : $Klass)

bb2(%5 : @guaranteed $Klass):
  end_borrow %5 : $Klass
  destroy_value %0 : $Klass
  %t = tuple ()
  return %t : $()
}

// CHECK-LABEL: sil [ossa] @test_simplify_term_with_identical_dest_blocks2 :
// CHECK: bb0
// CHECK: br bb2
// CHECK: bb2
// CHECK-LABEL: } // end sil function 'test_simplify_term_with_identical_dest_blocks2'
sil [ossa] @test_simplify_term_with_identical_dest_blocks2 : $@convention(thin) (@owned Klass) -> () {
bb0(%0 : @owned $Klass):
  test_specification "simplify-cfg-simplify-term-with-identical-dest-blocks @block[0]"
  br bb1(%0 : $Klass)

bb1(%3 : @owned $Klass):
  br bb2(%3 : $Klass)

bb2(%5 : @owned $Klass):
  destroy_value %5 : $Klass
  %t = tuple ()
  return %t : $()
}

